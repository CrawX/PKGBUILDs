# Maintainer: CruX <CruX@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

buildarch=4

pkgname=wandboard-brcmfmac-compat
pkgdesc="brcmfmac combined with wandboard firmware for ap mode on older kernels (namely 3.0.35)"
pkgver=3.13
pkgrel=1
arch=('armv7h')
install=${pkgname}.install
url="https://backports.wiki.kernel.org/index.php/Main_Page"
license=('GPL2' 'custom')
makedepends=('coccinelle' 'python2' 'git')
source=('git+git://git.kernel.org/pub/scm/linux/kernel/git/stable/linux-stable.git#linux-3.13.y'
        'git+git://git.kernel.org/pub/scm/linux/kernel/git/mcgrof/backports.git#linux-3.13.y'
        'config' 'http://www.wandboard.org/images/downloads/Driver_Release_4329.tgz')
md5sums=('SKIP'
         'SKIP'
         '2b838d33933a74bcf840ca2e64d514a3'
         '5c98fef2375224597bf1174c7dfccb90')

pkgver() {
   cd "${srcdir}/linux-stable"
   git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  # save newest commit sha for gentree
  cd "${srcdir}/linux-stable"
  _kernelrev=$(git rev-parse HEAD)

  msg2 "Generate tree for driver compilation"
  cd "${srcdir}/backports"
  python2.7 gentree.py --git-revision ${_kernelrev} --clean "${srcdir}/backports" "${srcdir}/backports-output"

  cd "${srcdir}/backports-output"

  cp "${srcdir}/config" .config

  unset CFLAGS

  make
}

package() {
  _kernver=$(uname -r)

  # set correct depmod command for install
  sed \
    -e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
    -i "${startdir}/${pkgname}.install"

  cd "${srcdir}/backports-output"

  # install modules
  for f in 'compat/compat.ko' 'net/wireless/cfg80211.ko' 'drivers/net/wireless/brcm80211/brcmutil/brcmutil.ko' 'drivers/net/wireless/brcm80211/brcmfmac/brcmfmac.ko'; do
    install -D -m0644 "${f}" "${pkgdir}/usr/lib/modules/${_kernver}/updates/${f}"
  done

  #install firmware
  install -D -m0644 "${srcdir}/${_firmware_packagename}/4329_Firmware_and_NVRAM/fw_4329.bin" "${pkgdir}/usr/lib/firmware/brcm/brcmfmac4329-sdio.bin"
  install -D -m0644 "${srcdir}/${_firmware_packagename}/4329_Firmware_and_NVRAM/nvram_4329.txt" "${pkgdir}/usr/lib/firmware/brcm/brcmfmac4329-sdio.txt"
}


# vim:set ts=2 sw=2 et:
